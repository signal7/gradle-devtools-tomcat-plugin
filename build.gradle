apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "idea"
apply plugin: "eclipse"

version = '0.1.0'
group = 'com.signal7.devtools'
def artifact = 'gradle-devtools-tomcat-plugin'

configurations {
    published.extendsFrom archives, signatures
}

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
}

jar {
    baseName = artifact
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    baseName = artifact
    classifier = 'sources'
    from sourceSets.main.allGroovy
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    description = 'An archive of the GroovyDocs for Maven Central'
    baseName = artifact
    classifier = 'javadoc'
    from fileTree(groovydoc.destinationDir)
}

artifacts {
    archives groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

install {
    repositories.mavenInstaller {
        pom.project(pomConfiguration)
    }
}

uploadPublished {
    dependsOn << [signArchives]
    repositories.mavenDeployer {
        beforeDeployment { deployment ->
            signing.signPom(deployment)
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: mavenCentralURL) {
            authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'never')
        }
        pom.project(pomConfiguration)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.7"
}

//
// A single place to create the POM configuration, so it can be
// applied from the install and the upload tasks.
//
def getPomConfiguration() {
    return {
        name('Gradle Signal7 Devtools Tomcat Plugin')
        description('A Gradle plugin for the Tomcat plugin of the Signal7 Devtools Eclipse Plugin.')
        url('https://github.com/signal7/gradle-devtools-deployment-plugin')
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id('mbruchmann')
                name('Max Bruchmann')
                email('bruchmann@signal7.de')
            }
            developer {
                id('aconstantin')
                name('Alexander Constantin')
                email('constantin@signal7.de')
            }
        }
        scm {
            connection('scm:https://github.com/signal7/gradle-devtools-tomcat-plugin.git')
            developerConnection('scm:git@github.com:signal7/gradle-devtools-tomcat-plugin.git')
            url('https://github.com/signal7/gradle-devtools-tomcat-plugin')
        }
    }
}
